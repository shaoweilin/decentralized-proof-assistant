{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e6571123",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Based on\n",
    "# https://gitlab.com/agentydragon/solid-flask/-/blob/master/solid_flask_main.py\n",
    "\n",
    "# This implementation is not secure.\n",
    "# Many required cryptographic checks were ignored.\n",
    "\n",
    "using HTTP\n",
    "using JSON\n",
    "using Base64\n",
    "using Random\n",
    "using SHA\n",
    "using Sockets\n",
    "using PyCall\n",
    "using Dates\n",
    "\n",
    "const pyjwk = pyimport(\"jwcrypto.jwk\")\n",
    "const pyjwt = pyimport(\"jwcrypto.jwt\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "33300119",
   "metadata": {},
   "outputs": [],
   "source": [
    "const _ISSUER        = \"https://inrupt.net/\"\n",
    "const _PORT          = 3333\n",
    "const _REDIRECT_PATH = \"/oauth/callback\"\n",
    "const _REDIRECT_URI  = \"http://localhost:\" * string(_PORT) * _REDIRECT_PATH\n",
    "const _AUTHORITY     = _ISSUER * \".well-known/openid-configuration\"\n",
    "\n",
    "const random_device = RandomDevice()\n",
    "const state_storage = Dict{String,Dict{String,String}}()\n",
    "const access_token  = Ref(\"\")\n",
    "const key           = Ref(\"\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a026b52f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function register_client(provider_info)\n",
    "    url     = provider_info[\"registration_endpoint\"]\n",
    "    headers = [\"content-type\" => \"application/json\"]\n",
    "    data    = Dict(\"redirect_uris\" => [_REDIRECT_URI])\n",
    "    result  = JSON.parse(String(HTTP.post(url, headers, JSON.json(data)).body))\n",
    "    @info \"registration response\" result\n",
    "    return result[\"client_id\"]\n",
    "end\n",
    "\n",
    "function make_random_string()\n",
    "    randstring(random_device, 50)\n",
    "end\n",
    "\n",
    "function make_verifier_challenge()\n",
    "    code_verifier  = make_random_string()\n",
    "    code_challenge = base64encode(sha256(code_verifier))\n",
    "    code_challenge = replace(replace(replace(code_challenge, \"+\" => \"-\"), \"/\" => \"_\"), \"=\" => \"\")\n",
    "    code_verifier, code_challenge\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cd2e1093",
   "metadata": {},
   "outputs": [],
   "source": [
    "const provider_info = JSON.parse(String(HTTP.request(\"GET\", _AUTHORITY).body))\n",
    "@info \"provider info\" provider_info\n",
    "\n",
    "const client_id = register_client(provider_info)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dd601d98",
   "metadata": {},
   "outputs": [],
   "source": [
    "function template(web_id, resource, resource_content)\n",
    "    web_str = web_id != \"\" ? \n",
    "        \"You are logged in as $(web_id).\" : \n",
    "        \"You are not logged in.\"\n",
    "    res_str = resource != \"\" ? \n",
    "        \"<pre>$(resource_content)</pre>\" : \n",
    "        \"Use the form below to read a resource.\"\n",
    "    \n",
    "    \"\"\"\n",
    "    <h2>Login status</h2>\n",
    "    $(web_str)\n",
    "    \n",
    "    <h2>Resource content</h2>\n",
    "    $(res_str)\n",
    "\n",
    "    <form action=/ method=GET>\n",
    "      <input\n",
    "          value=\"$(resource)\"\n",
    "          placeholder='https://you.solidcommunity.net/private/...'\n",
    "          name='resource'>\n",
    "      <input type=submit value=Read>\n",
    "    </form>\n",
    "    \"\"\"\n",
    "end\n",
    "\n",
    "function make_token_for(pykeypair, uri, method)\n",
    "    header = Dict(\n",
    "        \"typ\" => \"dpop+jwt\",\n",
    "        \"alg\" => \"ES256\",\n",
    "        \"jwk\" => pykeypair.export(private_key=false, as_dict=true)\n",
    "    )\n",
    "    claims = Dict(\n",
    "        \"jti\" => make_random_string(),\n",
    "        \"htm\" => method,\n",
    "        \"htu\" => uri,\n",
    "        \"iat\" => Int(floor(datetime2unix(now())))\n",
    "    )\n",
    "    jwt = pyjwt.JWT(header=header, claims=claims)\n",
    "    jwt.make_signed_token(pykeypair)\n",
    "    return jwt.serialize()\n",
    "end\n",
    "\n",
    "function index(req::HTTP.Request)\n",
    "    queryparams = HTTP.queryparams(HTTP.URI(req.target))\n",
    "    tested_url  = get(queryparams, \"resource\", \"\")\n",
    "\n",
    "    if access_token[] != \"\" && key[] != \"\"\n",
    "        println(\"loading access token and key from session\")\n",
    "        keypair = pyjwk.JWK.from_json(key[])\n",
    "        headers = [\n",
    "            \"authorization\" => \"DPoP \" * access_token[],\n",
    "            \"dpop\"          => make_token_for(keypair, tested_url, \"GET\")\n",
    "        ]\n",
    "        decoded_access_token = pyjwt.JWT(jwt=access_token[])\n",
    "        web_id = JSON.parse(decoded_access_token.token.objects[\"payload\"])[\"sub\"]\n",
    "    else\n",
    "        headers = Pair{String, String}[]\n",
    "        web_id = \"\"\n",
    "    end\n",
    "    \n",
    "    if tested_url != \"\"\n",
    "        resp = HTTP.get(tested_url, headers; status_exception = false)\n",
    "        if resp.status == 401\n",
    "            println(\"Got 401 trying to access \" * tested_url)\n",
    "            code_verifier, code_challenge = make_verifier_challenge()\n",
    "            state = make_random_string()\n",
    "            @assert !haskey(state_storage,state)\n",
    "            state_storage[state] = Dict(\"code_verifier\" => code_verifier, \"redirect_url\" => req.target)\n",
    "            \n",
    "            url = provider_info[\"authorization_endpoint\"] * \"?\" * HTTP.escapeuri([\n",
    "                \"code_challenge\"        => code_challenge,\n",
    "                \"state\"                 => state,\n",
    "                \"response_type\"         => \"code\",\n",
    "                \"redirect_uri\"          => _REDIRECT_URI,\n",
    "                \"code_challenge_method\" => \"S256\",\n",
    "                \"client_id\"             => client_id,\n",
    "                \"scope\"                 => \"openid offline_access\"\n",
    "            ])\n",
    "            return HTTP.Response(302,[\"Location\" => url])\n",
    "        elseif resp.status != 200\n",
    "            error(\"Unexpected status code: $(resp.status) $(resp.body)\")\n",
    "        end\n",
    "        resource_content = String(resp.body)\n",
    "    else\n",
    "        resource_content = \"\"\n",
    "    end\n",
    "    headers = [\"Content-Type\" => \"text/html\"]\n",
    "    return HTTP.Response(200, headers; body=template(web_id,tested_url,resource_content))\n",
    "end\n",
    "\n",
    "function oauth_callback(req::HTTP.Request)\n",
    "    queryparams = HTTP.queryparams(HTTP.URI(req.target))\n",
    "    auth_code   = get(queryparams, \"code\", \"\")\n",
    "    state       = get(queryparams, \"state\", \"\")\n",
    "    \n",
    "    @assert haskey(state_storage, state) \"state $(state) not in state_storage?\"\n",
    "    \n",
    "    pykeypair     = pyjwk.JWK.generate(kty=\"EC\", crv=\"P-256\")\n",
    "    key[]         = pykeypair.export()\n",
    "\n",
    "    state_info    = state_storage[state]\n",
    "    code_verifier = state_info[\"code_verifier\"]\n",
    "    redirect_url  = state_info[\"redirect_url\"]\n",
    "    \n",
    "    url     = provider_info[\"token_endpoint\"]\n",
    "    headers = [\n",
    "        \"dpop\" => make_token_for(pykeypair, url, \"POST\"),\n",
    "        \"content-type\" => \"application/x-www-form-urlencoded\"\n",
    "    ]\n",
    "    data    = HTTP.escapeuri([\n",
    "        \"grant_type\"    => \"authorization_code\",\n",
    "        \"client_id\"     => client_id,\n",
    "        \"redirect_uri\"  => _REDIRECT_URI,\n",
    "        \"code\"          => auth_code,\n",
    "        \"code_verifier\" => code_verifier\n",
    "    ])\n",
    "    result = JSON.parse(String(HTTP.post(url, headers, data; redirect=false).body))\n",
    "    access_token[] = result[\"access_token\"]\n",
    "\n",
    "    pop!(state_storage, state)\n",
    "    return HTTP.Response(302, [\"Location\" => redirect_url])\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "72aed848",
   "metadata": {},
   "outputs": [],
   "source": [
    "const ROUTER = HTTP.Router()\n",
    "HTTP.@register(ROUTER, \"GET\", \"/\", index)\n",
    "HTTP.@register(ROUTER, \"GET\", \"/oauth/callback\", oauth_callback)\n",
    "@async HTTP.serve(ROUTER, ip\"127.0.0.1\", _PORT)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1458f706",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.1",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
